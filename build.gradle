plugins {
    id 'war'
    id 'java'
    id 'jacoco'
    id 'application'
    id 'maven-publish'
    id 'de.undercouch.download' version '5.7.0'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'org.springframework.boot' version '4.0.2'
    id 'org.barfuin.gradle.jacocolog' version '4.0.1'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'com.github.dawnwords.jacoco.badge' version '0.2.4'
}

group = 'org.bbaw.aaew.tla'
version = '0.0.950-dev'
java {
    sourceCompatibility = JavaVersion.VERSION_17
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'tla-es'
            pom {
                name = 'TLA Elasticsearch Backend'
                description = 'Elasticsearch backend for the Thesaurus Linguae Aegyptiae web component'
            }
            from components.java
        }
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spring.io/milestone' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    implementation 'com.github.jkatzwinkel:tla-common:main-SNAPSHOT'
    implementation 'org.modelmapper:modelmapper:3.2.6'
    implementation 'org.apache.commons:commons-compress:1.28.0'
    implementation 'org.yaml:snakeyaml:2.5'

    implementation 'org.springframework.boot:spring-boot-starter-tomcat:4.0.2'
    implementation 'org.springframework.data:spring-data-elasticsearch:6.0.3'
    implementation 'org.springframework.boot:spring-boot-webmvc:4.0.2'
    implementation 'org.springframework:spring-webmvc:7.0.4'
    implementation 'org.springframework.boot:spring-boot-starter-logging:4.0.2'

    testImplementation('org.springframework.boot:spring-boot-starter-test:4.0.2') {
        exclude group: 'org.xmlunit', module: 'xmlunit-core'
        exclude group: 'jakarta.xml.bind'
    }
    testImplementation 'org.springframework.boot:spring-boot-webmvc-test:4.0.2'
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.2'
}

application {
    mainClass = 'tla.backend.App'
}

testlogger {
    theme = 'standard'
    slowThreshold = 1500
    showStandardStreams = false
    showFullStackTraces = true
}

test {
    useJUnitPlatform {
      includeTags '!search'
    }
    finalizedBy 'jacocoTestReport'
}

task testSearch(type: Test) {
    group = 'Verification'
    description = 'Runs search integration tests against populated ES instance.'
    systemProperty 'spring.profiles.active', 'search'
    useJUnitPlatform {
      includeTags 'search'
    }
    testClassesDirs = testing.suites.test.sources.output.classesDirs
    classpath = testing.suites.test.sources.runtimeClasspath
}

task testAll(type: Test) {
    group = 'Verification'
    description = 'Runs both search integration and unit tests.'
    useJUnitPlatform()
    testClassesDirs = testing.suites.test.sources.output.classesDirs
    classpath = testing.suites.test.sources.runtimeClasspath
    finalizedBy 'jacocoTestReport'
}

tasks.withType(Test) {
    environment 'ES_PORT', System.getenv('ES_PORT') ?: '9200'
    environment 'ES_HOST', System.getenv('ES_HOST') ?: 'localhost'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:deprecation']
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
    finalizedBy 'generateJacocoBadge'
}

jacocoLogTestCoverage {
    maxDecimalDigits = 2
}

springBoot {
    buildInfo()
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
    environment 'ES_PORT', System.getenv('ES_PORT') ?: '9200'
    environment 'ES_HOST', System.getenv('ES_HOST') ?: 'localhost'
}

task downloadSample(type: Download) {
    group = 'Init'
    description = 'Download corpus sample file'
    src System.getenv("SAMPLE_URL")
    dest new File('sample.tar.gz')
    onlyIfModified true
    outputs.files(
        file("sample.tar.gz")
    )
    onlyIf('corpus data file sample.tar.gz does not exist') {
        !file('sample.tar.gz').exists()
    }
}

clean {
    dependsOn 'cleanDownloadSample'
}

task populate {
    group = 'Init'
    description = 'Download corpus sample and ingest into database backend'
    inputs.files(
        file('sample.tar.gz')
    )
    dependsOn 'downloadSample'
    doLast {
        bootRun.args = ['--data-file=sample.tar.gz', '--shutdown']
    }
    finalizedBy 'bootRun'
}
