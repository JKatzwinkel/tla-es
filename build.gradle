plugins {
    id 'war'
    id 'java'
    id 'jacoco'
    id 'application'
    id 'maven-publish'
    id 'co.uzzu.dotenv.gradle' version '1.1.0'
    id 'de.undercouch.download' version '4.0.4'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.github.dawnwords.jacoco.badge' version '0.2.0'
}

group = 'org.bbaw.aaew.tla'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'tla-elasticsearch'
            pom {
                name = 'TLA Elasticsearch Backend'
                description = 'Elasticsearch backend for the Thesaurus Linguae Aegyptiae web component'
            }
            from components.java
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://jitpack.io' }
}

configurations {
    implementation.exclude module: 'spring-boot-starter-jetty'
    implementation.exclude module: 'spring-boot-starter-netty'
    implementation.exclude group: 'jakarta.xml.bind'
    implementation.exclude group: 'org.xmlunit'
    implementation.exclude group: 'org.skyscreamer'
    implementation.exclude module: 'jackson-dataformat-smile'
    implementation.exclude module: 'jackson-dataformat-cbor'
    // ES transport client stuff
    implementation.exclude module: 'transport'
    implementation.exclude module: 'transport-netty4-client'
    testImplementation.exclude module: 'junit-vintage-engine'
    testImplementation.exclude group: 'org.junit.vintage'
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation 'com.github.jkatzwinkel:tla-common:master-SNAPSHOT'
    implementation 'org.modelmapper:modelmapper:2.3.7'
    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'org.yaml:snakeyaml:1.25'

    implementation 'org.springframework.data:spring-data-elasticsearch:4.0.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot:2.3.0.M1'
    implementation 'org.springframework.boot:spring-boot-autoconfigure:2.3.0.M1'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.30'
    implementation 'org.springframework:spring-web:5.2.6.RELEASE'
    implementation 'org.springframework:spring-webmvc:5.2.6.RELEASE'
    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.4.RELEASE'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0-M1'
}

application {
    mainClassName = 'tla.backend.App'
}

test {
    useJUnitPlatform()
    environment "ES_PORT", env.ES_PORT.orElse("9200")
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    finalizedBy 'generateJacocoBadge'
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
    environment "ES_PORT", env.ES_PORT.orElse("9200")
    environment "ES_HOST", env.ES_HOST.orElse("localhost")
}

task downloadSample(type: Download) {
    group = 'Init'
    description = 'Download corpus sample file'
    src env.SAMPLE_URL.orElse("http://")
    dest new File('sample.tar.gz')
    onlyIfModified true
}

task populate {
    group = 'Init'
    description = 'Download corpus sample and ingest into database backend'
    doLast {
        bootRun.args = ['--data-file=sample.tar.gz', '--shutdown']
    }
    dependsOn 'downloadSample'
    finalizedBy 'bootRun'
}
