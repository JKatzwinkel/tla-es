buildscript {
  repositories {
    mavenCentral()
    // cf. https://github.com/JKatzwinkel/tla-es/pull/82
    // and more specifically https://github.com/gradle/gradle/issues/22864#issuecomment-1327978524
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

plugins {
    id 'war'
    id 'java'
    id 'jacoco'
    id 'application'
    id 'maven-publish'
    id 'co.uzzu.dotenv.gradle' version '2.0.0'
    id 'de.undercouch.download' version '5.5.0'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'org.springframework.boot' version '3.1.5'
    id 'org.barfuin.gradle.jacocolog' version '3.1.0'
    id 'com.github.ben-manes.versions' version '0.49.0'
    id 'com.github.dawnwords.jacoco.badge' version '0.2.4'
}

group = 'org.bbaw.aaew.tla'
version = '0.0.950-dev'
sourceCompatibility = '17'

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'tla-es'
            pom {
                name = 'TLA Elasticsearch Backend'
                description = 'Elasticsearch backend for the Thesaurus Linguae Aegyptiae web component'
            }
            from components.java
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'com.github.jkatzwinkel:tla-common:main-SNAPSHOT'
    implementation 'org.modelmapper:modelmapper:3.2.0'
    implementation 'org.apache.commons:commons-compress:1.25.0'
    implementation 'org.yaml:snakeyaml:2.2'

    implementation 'org.springframework.boot:spring-boot-starter-tomcat:3.2.0-RC2'
    implementation 'org.springframework.boot:spring-boot-autoconfigure:3.2.0-RC2'
    implementation 'org.springframework.data:spring-data-elasticsearch:5.2.0'
    implementation 'org.springframework:spring-webmvc:6.1.0-RC2'
    implementation 'org.springframework.boot:spring-boot-starter-logging:3.2.0-RC2'

    testImplementation('org.springframework.boot:spring-boot-starter-test:3.2.0-RC2') {
        exclude group: 'org.xmlunit', module: 'xmlunit-core'
        exclude group: 'jakarta.xml.bind'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
}

application {
    mainClass = 'tla.backend.App'
}

testlogger {
    theme 'standard'
    slowThreshold 1500
    showStandardStreams false
    showFullStackTraces true
}

test {
    useJUnitPlatform {
      includeTags '!search'
    }
    environment "ES_PORT", env.fetch("ES_PORT", "9200")
    finalizedBy 'jacocoTestReport'
}

task testSearch(type: Test) {
    group = 'Verification'
    description = 'Runs search integration tests against populated ES instance.'
    systemProperty 'spring.profiles.active', 'search'
    environment "ES_PORT", env.fetch("ES_PORT", "9200")
    useJUnitPlatform {
      includeTags 'search'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:deprecation']
}

task testAll(type: Test) {
    group = 'Verification'
    description = 'Runs both search integration and unit tests.'
    useJUnitPlatform()
    environment "ES_PORT", env.fetch("ES_PORT", "9200")
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
    finalizedBy 'generateJacocoBadge'
}

jacocoLogTestCoverage {
    maxDecimalDigits = 2
}

springBoot {
    buildInfo()
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
    environment "ES_PORT", env.fetch("ES_PORT", "9200")
    environment "ES_HOST", env.fetch("ES_HOST", "localhost")
}

task downloadSample(type: Download) {
    group = 'Init'
    description = 'Download corpus sample file'
    src env.fetch("SAMPLE_URL", "http://")
    dest new File('sample.tar.gz')
    onlyIfModified true
    outputs.files(
        file("sample.tar.gz")
    )
}

clean {
    dependsOn 'cleanDownloadSample'
}

task populate {
    group = 'Init'
    description = 'Download corpus sample and ingest into database backend'
    doLast {
        bootRun.args = ['--data-file=sample.tar.gz', '--shutdown']
    }
    dependsOn 'downloadSample'
    finalizedBy 'bootRun'
}
